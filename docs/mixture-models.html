<!DOCTYPE html>
<html>
  <head>
    <title>ProbMods: Mixture models</title>
    <meta http-equiv="Content-Type" content="text/html; charset=UTF-8" />
    <meta http-equiv="Cache-Control" content="no-cache, no-store, must-revalidate" />
    <meta http-equiv="Pragma" content="no-cache" />
    <meta http-equiv="Expires" content="0" />
    <link rel="stylesheet" type="text/css" href="style.css">
    <link rel="stylesheet" type="text/css" href="webchurch/online/css/codemirror.css">
    <link rel="stylesheet" type="text/css" href="webchurch/online/css/d3.css">
    <link rel="shortcut icon" href="images/favicon.ico" />
    <script src="scripts/underscore-min.js"></script>
    <script src="scripts/jquery.js"></script>
    <link class="katex-include" rel="stylesheet" type="text/css" href="//cdnjs.cloudflare.com/ajax/libs/KaTeX/0.5.1/katex.min.css">
    <script class="katex-include" src="//cdnjs.cloudflare.com/ajax/libs/KaTeX/0.5.1/katex.min.js"></script>
    <script src="scripts/cookies-0.3.1.min.js"></script>
    <script src="scripts/gg.js"></script>
    <script src="scripts/nav.js"></script>
    <script src="scripts/cosmetics.js"></script>
    <script src="scripts/md5.js"></script>
    <script src="webchurch/online/webchurch.min.js"></script>
    <script src="scripts/globals.js"></script>
    <script src="webchurch/online/vega.min.js"></script>
    <script src="scripts/new-injector.js"></script>
    <script src="scripts/headroom.min.js"></script>
  </head>
<body>
<div id="chapter-wrapper">
  <div id='header' class="headroom">
    <div id='logotype'><a href="index.html">Probabilistic Models of Cognition</a></div>
    <ul id="nav">
      <span class="nav0"><li class="all-chapters">All chapters
<ol start="0">
<a href="index.html"><li class="nonum">Index</li></a>
<a href="introduction.html"><li>Introduction</li></a>
<a href="generative-models.html"><li>Generative models</li></a>
<a href="conditioning.html"><li>Conditioning</li></a>
<a href="patterns-of-inference.html"><li>Patterns of inference</li></a>
<a href="observing-sequences.html"><li>Models for sequences of observations</li></a>
<a href="inference-about-inference.html"><li>Inference about inference</li></a>
<a href="inference-process.html"><li>Algorithms for inference</li></a>
<a href="learning-as-conditional-inference.html"><li>Learning as conditional inference</li></a>
<a href="hierarchical-models.html"><li>Hierarchical models</li></a>
<a href="occam's-razor.html"><li>Occam's Razor</li></a>
<a href="mixture-models.html"><li>Mixture models</li></a>
<a href="non-parametric-models.html"><li>Non-parametric models</li></a>
<a href="appendix-scheme.html"><li>Appendix: Scheme basics</li></a>
<a href="webchurch/online/ref.html"><li>Church Reference</li></a>
</ol>
      </li></span>
      <a class="nav0" href="/login"><li id="login-link">Login</li></a>
      <!-- <a class="nav0" href="/profile"><li id="profile-link" style='display: none'>Profile</li></a>  -->
      <a class="nav0" href="/logout"><li id="logout-link" style='display: none'>Logout</li></a>
    </ul>
    <div class="clear"></div>
  </div>

  <div id="chapter">
<h1 id="chapter-title">11. Mixture models</h1>
<div class='toc'>
<div class='name'>Contents:</div>
<ul>
<li><a href="#learning-categories">Learning Categories</a><ul>
<li><a href="#example-topic-models">Example: Topic Models</a></li>
<li><a href="#example-categorical-perception-of-speech-sounds">Example: Categorical Perception of Speech Sounds</a></li>
</ul></li>
<li><a href="#unknown-numbers-of-categories">Unknown Numbers of Categories</a></li>
</ul>
</div>
<p>In the chapter on <a href="hierarchical-models.html">Hierarchical Models</a>, we saw the power of probabilistic inference in learning about the latent structure underlying different kinds of observations: the mixture of colors in different bags of marbles, or the prototypical features of categories of animals. In that discussion we always assumed that we knew what kind each observation belonged to—the bag that each marble came from, or the subordinate, basic, and superordinate category of each object. Knowing this allowed us to pool the information from each observation for the appropriate latent variables. What if we don’t know <em>a priori</em> how to divide up our observations? In this chapter we explore the problem of simultaneously discovering kinds and their properties – this can be done using <em>mixture models</em>.</p>
<h1 id="learning-categories"><a href="#learning-categories">Learning Categories</a></h1>
<p>Imagine a child who enters the world and begins to see objects. She can’t begin by learning the typical features of cats or mice, because she doesn’t yet know that there are such kinds of objects as cats and mice. Yet she may quickly notice that some of the objects all tend to purr and have claws, while other objects are small and run fast—she can <em>cluster</em> the objects together on the basis of common features and thus form categories (such as cats and mice), whose typical features she can then learn.</p>
<p>To formalize this learning problem, we begin by adapting the bags-of-marbles examples from the <a href="hierarchical-models.html">Hierarchical Models</a> chapter. However, we now assume that the bag that each marble is drawn from is <em>unobserved</em> and must be inferred.</p>
<pre><code>(define colors &#39;(blue green red))

(define samples
 (mh-query
   200 100

   (define phi (dirichlet &#39;(1 1 1)))
   (define alpha 0.1)
   (define prototype (map (lambda (w) (* alpha w)) phi))

   (define bag-&gt;prototype (mem (lambda (bag) (dirichlet prototype))))

   ;;each observation (which is named for convenience) comes from one of three bags:
   (define obs-&gt;bag
     (mem (lambda (obs-name)
            (uniform-draw &#39;(bag1 bag2 bag3)))))

   (define draw-marble
     (mem (lambda (obs-name)
            (multinomial colors (bag-&gt;prototype (obs-&gt;bag obs-name))))))

   ;;did obs1 and obs2 come from the same bag? obs1 and obs3?
   (list (equal? (obs-&gt;bag &#39;obs1) (obs-&gt;bag &#39;obs2))
         (equal? (obs-&gt;bag &#39;obs1) (obs-&gt;bag &#39;obs3)))

   (and
    (equal? &#39;red (draw-marble &#39;obs1))
    (equal? &#39;red (draw-marble &#39;obs2))
    (equal? &#39;blue (draw-marble &#39;obs3))
    (equal? &#39;blue (draw-marble &#39;obs4))
    (equal? &#39;red (draw-marble &#39;obs5))
    (equal? &#39;blue (draw-marble &#39;obs6))
    )))

(hist (map first samples) &quot;obs1 and obs2 same category?&quot;)
(hist (map second samples) &quot;obs1 and obs3 same category?&quot;)
&#39;done</code></pre>
<p>We see that it is likely that <code>obs1</code> and <code>obs2</code> came from the same bag, but quite unlikely that <code>obs3</code> did. Why? Notice that we have set <code>alpha</code> small, indicating a belief that the marbles in a bag will tend to all be the same color. How do the results change if you make <code>alpha</code> larger? Why? Note that we have queried on whether observed marbles came out of the same bag, instead of directly querying on the bag number that an observation came from. This is because the bag number by itself is meaningless—it is only useful in its role of determining which objects have similar properties. Formally, the model we have defined above is symmetric in the bag labels (if you permute all the labels you get a new state with the same probability).</p>
<p>Instead of assuming that a marble is equally likely to come from each bag, we could instead learn a distribution over bags where each bag has a different probability. This is called a <em>mixture distribution</em> over the bags:</p>
<pre><code>(define colors &#39;(blue green red))

(define samples
 (mh-query
   200 100

   (define phi (dirichlet &#39;(1 1 1)))
   (define alpha 0.1)
   (define prototype (map (lambda (w) (* alpha w)) phi))

   (define bag-&gt;prototype (mem (lambda (bag) (dirichlet prototype))))

   ;;the probability that an observation will come from each bag:
   (define bag-mixture (dirichlet &#39;(1 1 1)))

   ;;each observation (which is named for convenience) comes from one of three bags:
   (define obs-&gt;bag
     (mem (lambda (obs-name)
            (multinomial &#39;(bag1 bag2 bag3) bag-mixture))))

   (define draw-marble
     (mem (lambda (obs-name)
            (multinomial colors (bag-&gt;prototype (obs-&gt;bag obs-name))))))

   ;;did obs1 and obs2 come from the same bag? obs1 and obs3?
   (list (equal? (obs-&gt;bag &#39;obs1) (obs-&gt;bag &#39;obs2))
         (equal? (obs-&gt;bag &#39;obs1) (obs-&gt;bag &#39;obs3)))

   (and
    (equal? &#39;red (draw-marble &#39;obs1))
    (equal? &#39;red (draw-marble &#39;obs2))
    (equal? &#39;blue (draw-marble &#39;obs3))
    (equal? &#39;blue (draw-marble &#39;obs4))
    (equal? &#39;red (draw-marble &#39;obs5))
    (equal? &#39;blue (draw-marble &#39;obs6))
    )))

(hist (map first samples) &quot;obs1 and obs2 same category?&quot;)
(hist (map second samples) &quot;obs1 and obs3 same category?&quot;)
&#39;done</code></pre>
<p>Models of this kind are called <strong>mixture models</strong> because the observations are a “mixture” of several categories. Mixture models are widely used in modern probabilistic modeling because they describe how to learn the unobservable categories which underlie observable properties in the world.</p>
<p>The observation distribution associated with each mixture <em>component</em> (i.e., kind or category) can be any distribution we like. For example, here is a mixture model with <em>Gaussian</em> components:</p>
<pre><code>(define samples
  (mh-query
   200 100

   (define bag-mixture (dirichlet &#39;(1 1)))

   (define obs-&gt;cat 
     (mem (lambda (obs-name) 
            (multinomial &#39;(bag1 bag2) bag-mixture))))

   (define cat-&gt;mean (mem (lambda (cat) (list (gaussian 0.0 1.0) (gaussian 0.0 1.0)))))

   (define (observe-point obs-name value) 
     (map (lambda (x-mean y) (condition (equal? (gaussian x-mean 0.01) y)))
          (cat-&gt;mean (obs-&gt;cat obs-name))
          value))

   ;; look at where bag1 and bag2 centers are
   (map cat-&gt;mean &#39;(bag1 bag2))

   ;; one cluster of points in the top right quadrant
   (observe-point &#39;a1 &#39;(0.5 0.5))
   (observe-point &#39;a2 &#39;(0.6 0.5))   
   (observe-point &#39;a3 &#39;(0.5 0.4))   
   (observe-point &#39;a4 &#39;(0.55 0.55)) 
   (observe-point &#39;a5 &#39;(0.45 0.45)) 
   (observe-point &#39;a6 &#39;(0.5 0.5))   
   (observe-point &#39;a7 &#39;(0.7 0.6))    

   ;; another cluster of points in the lower left quadrant
   (observe-point &#39;b1 &#39;(-0.5 -0.5))
   (observe-point &#39;b2 &#39;(-0.7 -0.4)) 
   (observe-point &#39;b3 &#39;(-0.5 -0.6)) 
   (observe-point &#39;b4 &#39;(-0.55 -0.55))
   (observe-point &#39;b5 &#39;(-0.5 -0.45))
   (observe-point &#39;b6 &#39;(-0.6 -0.5))
   (observe-point &#39;b7 &#39;(-0.6 -0.4))))

(scatter (map first samples) &quot;bag 1 mean&quot;)
(scatter (map second samples) &quot;bag 2 mean&quot;)</code></pre>
<h2 id="example-topic-models"><a href="#example-topic-models">Example: Topic Models</a></h2>
<p>One very popular class of mixture-based approaches are <em>topic models</em>, which are used for document classification, clustering, and retrieval. The simplest kind of topic models make the assumption that documents can be represented as <em>bags of words</em> — unordered collections of the words that the document contains. In topic models, each document is associated with a mixture over <em>topics</em>, each of which is itself a distribution over words.</p>
<p>One popular kind of bag-of-words topic model is known as <em>Latent Dirichlet Allocation</em> (LDA).<ref>Blei, David M.; Ng, Andrew Y.; Jordan, Michael I (January 2003). Latent Dirichlet allocation. Journal of Machine Learning Research 3: pp. 993–1022.</ref> The generative process for this model can be described as follows. For each document, mixture weights over a set of <span class="math">\(K\)</span> topics are drawn from a Dirichlet prior. Then <span class="math">\(N\)</span> topics are sampled for the document—one for each word. Each topic itself is associated with a distribution over words, and this distribution is drawn from a Dirichlet prior. For each of the <span class="math">\(N\)</span> topics drawn for the document, a word is sampled from the corresponding multinomial distribution. This is shown in the Church code below.</p>
<pre><code>(define vocabulary (append &#39;(DNA evolution)&#39;(parsing phonology)))

(define topics &#39;(topic1 topic2))
(define doc-length 10)

(define samples
  (mh-query
   200
   100

   (define document-&gt;length (mem (lambda (doc-id) doc-length)))
   (define document-&gt;mixture-params (mem (lambda (doc-id) (dirichlet (make-list (length topics) 1.0)))))
   (define topic-&gt;mixture-params (mem (lambda (topic) (dirichlet (make-list (length  vocabulary) 0.1)))))


   (define document-&gt;topics (mem (lambda (doc-id)
                                   (repeat  (document-&gt;length doc-id)
                                            (lambda () (multinomial topics (document-&gt;mixture-params doc-id)))))))

   (define document-&gt;words (mem (lambda (doc-id) (map (lambda (topic)
                                                        (multinomial vocabulary (topic-&gt;mixture-params topic)))
                                                      (document-&gt;topics doc-id)))))

   (define (observe-document doc-id words)
     (define topics (document-&gt;topics doc-id))
     (define topic-mixtures (map topic-&gt;mixture-params topics)) 
     (map
      (lambda (topic-mixture word) (condition (equal? (multinomial vocabulary topic-mixture) word)))
      topic-mixtures words))

   ;; get the distributions over words for the two topics
   (list (topic-&gt;mixture-params &#39;topic1) (topic-&gt;mixture-params &#39;topic2))

   (observe-document &#39;a1 &#39;(DNA evolution DNA evolution DNA evolution DNA evolution DNA evolution))
   (observe-document &#39;a2 &#39;(DNA evolution DNA evolution DNA evolution DNA evolution DNA evolution))
   (observe-document &#39;a3 &#39;(DNA evolution DNA evolution DNA evolution DNA evolution DNA evolution))
   
   (observe-document &#39;b1 &#39;(parsing phonology parsing phonology parsing phonology parsing phonology parsing phonology))
   (observe-document &#39;b2 &#39;(parsing phonology parsing phonology parsing phonology parsing phonology parsing phonology))
   (observe-document &#39;b3 &#39;(parsing phonology parsing phonology parsing phonology parsing phonology parsing phonology))
   ))

(define (list-add x y)
  (map + x y))

;; add rows of a list of lists (i.e., matrix)
(define (mat-row-sum m)
  (if (= (length m) 1)
      (first m)
      (mat-row-sum (pair (list-add (first m) (second m))
                         (rest (rest m))))))

;; get mean of a list of lists (i.e., matrix)
(define (mat-row-mean m)
  (define n (length m))
  (map (lambda (x) (/ x n))
       (mat-row-sum m))) 
            
(barplot (list vocabulary (mat-row-mean (map first samples))) &quot;Distribution over words for Topic 1&quot;)
(barplot (list vocabulary (mat-row-mean (map second samples))) &quot;Distribution over words for Topic 2&quot;)</code></pre>
<p>In this simple example, there are two topics <code>topic1</code> and <code>topic2</code>, and four words. These words are deliberately chosen to represent on of two possible subjects that a document can be about: One can be thought of as ‘biology’ (i.e., <code>DNA</code> and <code>evolution</code>), and the other can be thought of as ‘linguistics’ (i.e., <code>parsing</code> and <code>syntax</code>).</p>
<p>The documents consist of lists of individual words from one or the other topic. Based on the coocurrence of words within individual documents, the model is able to learn that one of the topics should put high probability on the biological words and the other topic should put high probability on the linguistic words. It is able to learn this because different kinds of documents represent stable mixture of different kinds of topics which in turn represent stable distributions over words.</p>
<h2 id="example-categorical-perception-of-speech-sounds"><a href="#example-categorical-perception-of-speech-sounds">Example: Categorical Perception of Speech Sounds</a></h2>
<!--
### Learning of Phone Categories by Infants


(Adapted from: Maye, J., Werker, J. F., and Gerken, L. (2002). Infant sensitivity to distributional information can affect phonetic discrimination. Cognition, 82:B101–B111.)

<img src='Maye.png' />

~~~~ {data-engine="mit-church"}
(define (noisy=? x y) (and (flip (expt 0.1 (abs (- (first x) (first y)))))
                           (flip (expt 0.1 (abs (- (rest x) (rest y)))))))
(define samples
 (mh-query
   200 100

   (define bag-mixture (dirichlet '(1 1 1)))

   (define obs->cat
     (mem (lambda (obs-name)
            (multinomial '(bag1 bag2 bag3) bag-mixture))))

   (define cat->mean (mem (lambda (cat) (list (gaussian 0.0 1.0) (gaussian 0.0 1.0)))))

   (define observe
     (mem (lambda (obs-name)
            (pair (gaussian (first (cat->mean (obs->cat obs-name))) 0.01)
                  (gaussian (second (cat->mean (obs->cat obs-name))) 0.01)))))

   ;;sample a new observations and its category
   (list (obs->cat 't) (observe 't))

   (no-proposals
   (and
    (noisy=? '(0.5 . 0.5) (observe 'a1))
    (noisy=? '(0.6 . 0.5) (observe 'a2))
    (noisy=? '(0.5 . 0.4) (observe 'a3))
    (noisy=? '(0.55 . 0.55) (observe 'a4))
    (noisy=? '(0.45 . 0.45) (observe 'a5))
    (noisy=? '(0.5 . 0.5) (observe 'a6))
    (noisy=? '(0.7 . 0.6) (observe 'a7))


    (noisy=? '(-0.5 . -0.5) (observe 'b1))
    (noisy=? '(-0.7 . -0.4) (observe 'b2))
    (noisy=? '(-0.5 . -0.6) (observe 'b3))
    (noisy=? '(-0.55 . -0.55) (observe 'b4))
    (noisy=? '(-0.5 . -0.45) (observe 'b5))
    (noisy=? '(-0.6 . -0.5) (observe 'b6))
    (noisy=? '(-0.6 . -0.4) (observe 'b7))
    ))))

(scatter (map second samples) "predictive")
'done
~~~~
-->


<p>(This example is adapted from: Feldman, N. H., Griffiths, T. L., and Morgan, J. L. (2009). The influence of categories on perception: Explaining the perceptual magnet effect as optimal statistical inference. Psychological Review, 116(4):752–782.)</p>
<p>Human perception is often skewed by our expectations. A common example of this is called <em>categorical perception</em> – when we perceive objects as being more similar to the category prototype than they really are. In phonology this is been particularly important and is called the perceptual magnet effect: Hearers regularize a speech sound into the category that they think it corresponds to. Of course this category isn’t known a priori, so a hearer must be doing a simultaneous inference of what category the speech sound corresponded to, and what the sound must have been. In the below code we model this as a mixture model over the latent categories of sounds, combined with a noisy observation process.</p>
<pre><code>(define (count-by start end increment)
  (if (&gt; start end)
      &#39;()
      (pair start (count-by (+ start increment) end increment))))

(define (expectation l)
  (/ (apply + l) (length l)))

(define prototype-1 8.0)
(define prototype-2 10.0)

(define (compute-pair-distance stimulus-1 stimulus-2)
  (expectation
   (mh-query
    2000 10

    (define (vowel-1) (gaussian prototype-1 0.5))
    (define (vowel-2) (gaussian prototype-2 0.5))

    (define (noise-process target)
      (gaussian target 0.2))

    (define (sample-target)
      (if (flip)
          (vowel-1)
          (vowel-2)))

    (define target-1 (sample-target))
    (define target-2 (sample-target))

    (define obs-1 (noise-process target-1))
    (define obs-2 (noise-process target-2))

    (abs (- target-1 target-2))

    ;;Condition on the targets being equal to the stimuli through a gaussian noise process
    (and 
      (= stimulus-1 (gaussian target-1 0.2))
      (= stimulus-2 (gaussian target-2 0.2))))))

(define (compute-perceptual-pairs list)
  (if (&lt; (length list) 2)
      &#39;()
      (pair (compute-pair-distance (first list) (second list)) (compute-perceptual-pairs (rest list)))))

(define (compute-stimuli-pairs list)
  (if (&lt; (length list) 2)
      &#39;()
      (pair (abs (- (first list) (second list))) (compute-stimuli-pairs (rest list)))))

(define stimuli (count-by prototype-1 prototype-2 0.1))

(define stimulus-distances (compute-stimuli-pairs stimuli))
(define perceptual-distances (compute-perceptual-pairs stimuli))

(scatter (map pair (iota (- (length stimuli) 1))
              stimulus-distances)
         &quot;Stimulus Distances&quot;)

(scatter (map pair (iota (- (length stimuli) 1))
              perceptual-distances)
         &quot;Perceptual Distances&quot;)</code></pre>
<p>Notice that the perceived distances between input sounds are skewed relative to the actual acoustic distances – that is they are attracted towards the category centers.</p>
<p><img src='images/Pme.png' /></p>
<h1 id="unknown-numbers-of-categories"><a href="#unknown-numbers-of-categories">Unknown Numbers of Categories</a></h1>
<p>The models above describe how a learner can simultaneously learn which category each object belongs to, the typical properties of objects in that category, and even global parameters about kinds of objects in general. However, it suffers from a serious flaw: the number of categories was fixed. This is as if a learner, after finding out there are cats, dogs, and mice, must force an elephant into one of these categories, for want of more categories to work with.</p>
<p>The simplest way to address this problem, which we call <em>unbounded</em> models, is be to simply place uncertainty on the number of categories in the form of a hierarchical prior. Let’s warm up with a simple example of this: inferring whether one or two coins were responsible for a set of outcomes (i.e. imagine a friend is shouting each outcome from the next room–“heads, heads, tails…”–is she using a fair coin, or two biased coins?).</p>
<pre><code>(define actual-obs (list true true true true false false false false))

(define samples
 (mh-query
   200 100

   (define coins (if (flip) &#39;(c1) &#39;(c1 c2)))

   (define coin-&gt;weight (mem (lambda (c) (uniform 0 1))))

   (define (observe values)
     (map (lambda (v)
       (condition (equal? (flip (coin-&gt;weight (uniform-draw coins))) v))) values))

   (length coins)

   (observe actual-obs)))

(hist samples &quot;number of coins&quot;)
&#39;done</code></pre>
<p>How does the inferred number of coins change as the amount of data grows? Why?</p>
<p>We could extend this model by allowing it to infer that there are more than two coins. However, no evidence requires us to posit three or more coins (we can always explain the data as “a heads coin and a tails coin”). Instead, let us apply the same idea to the marbles examples above:</p>
<pre><code>(define colors &#39;(blue green red))

(define samples
  (mh-query
   200 100

   (define phi (dirichlet &#39;(1 1 1)))
   (define alpha 0.1)
   (define prototype (map (lambda (w) (* alpha w)) phi))

   (define bag-&gt;prototype (mem (lambda (bag) (dirichlet prototype))))

   ;;unknown number of categories (created with placeholder names):
   (define num-bags (+ 1 (poisson 1.0)))

   (define bags (repeat num-bags gensym))

   (define (observe marbles)
     (map (lambda (m) (condition (equal? (multinomial colors (bag-&gt;prototype (uniform-draw bags))) m))) marbles))

   ;;how many bags are there?
   num-bags

   (observe &#39;(red red blue blue red blue))))

(hist samples &quot;how many bags?&quot;)
&#39;done</code></pre>
<p>Vary the amount of evidence and see how the inferred number of bags changes.</p>
<p>For the prior on <code>num-bags</code> we used the <a href="http://en.wikipedia.org/wiki/Poisson_distribution"><em>Poisson distribution</em></a> which is a distribution on non-negative integers. It is convenient, though implies strong prior knowledge (perhaps too strong for this example). We have created <code>gensym</code> functions using <code>make-gensym</code>; a <code>gensym</code> function returns a fresh symbol every time it is called. It can be used to generate an unbounded set of labels for things like classes, categories and mixture components. Each evaluation of <code>gensym</code> results in a unique (although cryptic) symbol:</p>
<pre><code>(list (gensym) (gensym) (gensym))</code></pre>
<p>Importantly, these symbols can be used as identifiers, because two different calls to gensym will never be equal:</p>
<pre><code>(equal? (gensym) (gensym))</code></pre>
<p>For clarity, you can use the <code>make-gensym</code> to create a <code>gensym</code> function with a custom prefix:</p>
<pre><code>(define my-gensym (make-gensym &quot;foo&quot;))
(list (my-gensym) (my-gensym) (my-gensym))</code></pre>
<p>Unbounded models give a straightforward way to represent uncertainty over the number of categories in the world. However, inference in these models often presents difficulties. In the next section we describe another method for allowing an unknown number of things: In an unbounded model, there are a finite number of categories whose number is drawn from an unbounded prior distribution, such as the Poisson prior that we just examined. In an ‘infinite model’ we construct distributions assuming a truly infinite numbers of objects.</p>
   </div>
</div>
</body>

<script src="scripts/after-body.js"></script>
</html>
